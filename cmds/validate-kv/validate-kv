#!/usr/bin/env -S awk -f ${_} --

function usage() {
    print "validate-kv - validate key-value format data"
    print ""
    print "Usage: validate-kv <rule file> <data file>"
    print "Version: fujis Tue Nov 26 18:27:03 JST 2024"
    exit 1
}

BEGIN {
  # Parse Options
  for (i = 1; i < ARGC; i++) {
    if ( ARGV[i] == "--") {
      break
    } else if ( \
      ARGV[i] == "-h"         ||
      ARGV[i] == "--help"     ||
      ARGV[i] == "-v"         ||
      ARGV[i] == "--version"  ||
      ARGC == 1               \
    ) {
      usage()
    }
  }
  # remove script name
  delete ARGV[1]
}

######################################################################
# Main Routine
BEGIN {
  exit_code = 0
}

# Skip comments and empty lines
/^#/ || NF == 0 {
    next
}

# Parse Rule
NR == FNR {
  key = $1; value = substr($0, index($0, $2))
  # Optional rule can be just ignored because this
  # program handles all fileds as optional in default.
  gsub(/optional/, "", value)
  if (gsub(/required/, "", value)) {
    required[key] = 1
  }
  rule_map[key] = value
  next
}

# Validate data
{
  key = $1; value = substr($0, index($0, $2))

  # todo: error when ignore option is not set
  if (!(key in rule_map)) next

  delete required[key]

  n = split(rule_map[key], rule_list, " ")

  for (i = 1; i <= n; i++) {
    rule = rule_list[i]
    invalid_rule = ""

    if(rule == "int") {
      if (value !~ /^-?[0-9]+$/) {
        invalid_rule = invalid_rule " int"
      }
    } else if(rule == "number") {
      if (value !~ /^-?[0-9]+(\.[0-9]+)?$/) {
        invalid_rule = invalid_rule " number"
      }
    } else if(rule == "digit") {
      if (value !~ /^[0-9]+$/) {
        invalid_rule = invalid_rule " digit"
      }
    } else if(rule == "email") {
      if (value !~ /^[a-zA-Z0-9._%+-]+@[a-zA-Z0-9.-]+\.[a-zA-Z]{2,}$/) {
        invalid_rule = invalid_rule " email"
      }
    } else if(rule == "alnum") {
      if (value !~ /^[a-zA-Z0-9]+$/) {
        invalid_rule = invalid_rule " alnum"
      }
    } else if(rule == "alpha") {
      if (value !~ /^[a-zA-Z]+$/) {
        invalid_rule = invalid_rule " alpha"
      }
    } else if(rule == "min_length") {
      if (length(value) < rule_list[++i]) {
        invalid_rule = invalid_rule " min_length " rule_list[i]
      }
    } else if(rule == "max_length") {
      if (length(value) > rule_list[++i]) {
        invalid_rule = invalid_rule " max_length " rule_list[i]
      }
    } else if (rule == "min_value") {
      if (value + 0 < rule_list[++i] + 0) {
        invalid_rule = invalid_rule " min_value " rule_list[i]
      }
    } else if (rule == "max_value") {
      if (value + 0 > rule_list[++i] + 0) {
        invalid_rule = invalid_rule " max_value " rule_list[i]
      }
    } else {
      invalid_rule = invalid_rule " unknown_rule " rule
    }

    if (invalid_rule != "") {
      print key invalid_rule
      exit_code = 1
    }
  }
}

END {
  for (key in required) {
    print key " required"
    exit_code = 1
  }
  exit exit_code
}
